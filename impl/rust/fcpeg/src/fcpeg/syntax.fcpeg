[Main]{
    + start Syntax.FCPEG,
}

[Syntax]{
    + use Block,
    + use Symbol,

    FCPEG <- Symbol.Space*# Symbol.LineEnd*# (Block.Block Symbol.LineEnd+#)* Symbol.LineEnd*# Symbol.Space*# Symbol.EOF#,
}

[Symbol]{
    Space <- " ",
    LineEnd <- Space* "\n" Space*,
    Div <- Space : "\n",
    CommaDiv <- Div* (",," LineEnd Div* : "," Space*),
    EOF <- "\z",
}

[Misc]{
    SingleID <- [a-zA-Z_] [a-zA-Z0-9_]*,
    ChainID <- SingleID ("."# SingleID)*##,
}

[Block]{
    + use Misc,
    + use Rule,
    + use Symbol,

    Block <- "["# Symbol.Div*# Misc.SingleID Symbol.Div*# "]"# Symbol.Div*# "{"# Symbol.LineEnd+# (Cmd Symbol.LineEnd+#)* "}"#,
    Cmd <- CommentCmd : DefineCmd : StartCmd : UseCmd,
    CommentCmd <- "%"# (!"," . : ",,")*## ","#,
    DefineCmd <- Misc.SingleID DefineCmdGenericsIDs? DefineCmdFuncIDs? Symbol.Div*# "<-"# Symbol.Div*# Rule.PureChoice Symbol.Div*# ","#,
    DefineCmdGenericsIDs <- Symbol.Div*# "<"# Symbol.Div*# Rule.ArgID (Symbol.Div*# ","# Symbol.Div*# Rule.ArgID)*## Symbol.Div*# ">"# Symbol.Div*#,
    DefineCmdFuncIDs <- Symbol.Div*# "("# Symbol.Div*# Rule.ArgID (Symbol.Div*# ","# Symbol.Div*# Rule.ArgID)*## Symbol.Div*# ")"# Symbol.Div*#,
    StartCmd <- "+"# Symbol.Div*# "start"# Symbol.Div+# Misc.ChainID Symbol.Div*# ","#,
    UseCmd <- "+"# Symbol.Div*# "use"# Symbol.Div+# Misc.ChainID UseCmdBlockAlias? Symbol.Div*# ","#,
    UseCmdBlockAlias <- Symbol.Div+# "as" Symbol.Div+# Misc.SingleID,
}

[Rule]{
    + use Misc,
    + use Symbol,

    InstantPureChoice <- Seq ("," Symbol.Space# Seq)*##,
    PureChoice <- Seq ((Symbol.Div+# ":" Symbol.Div+# : ",")## Seq)*##,
    Choice <- "("# PureChoice ")"#,

    Seq <- SeqElem (Symbol.Div+# SeqElem)*##,
    % (Choice : Expr) に命名する,
    SeqElem <- Lookahead? (Choice : Expr) Loop? RandomOrder? ASTReflectionStyle?,

    Expr <- Generics : ArgID : Func : ID : Str : CharClass : Wildcard,

    Lookahead <- "!" : "&",
    Loop <- "?" : "*" : "+" : LoopRange,
    LoopRange <- "{"# Symbol.Div*# Num?#MinNum (Symbol.CommaDiv# Num?#MaxNum)?#MaxNumGroup Symbol.Div*# "}"#,
    RandomOrder <- "^"# RandomOrderRange?,
    RandomOrderRange <- "["# Symbol.Div*# Num? Symbol.CommaDiv# Num? Symbol.Div*# "]"#,
    ASTReflectionStyle <- "##" : "#"# Misc.SingleID?##,

    Num <- [0-9]+,
    ID <- Misc.ChainID,
    ArgID <- "$"# Misc.SingleID##,
    Generics <- Misc.ChainID "<"# Symbol.Div*# InstantPureChoice (Symbol.Div*# ","# Symbol.Div*# InstantPureChoice)*## Symbol.Div*# ">"#,
    Func <- Misc.ChainID "("# Symbol.Div*# InstantPureChoice (Symbol.Div*# ","# Symbol.Div*# InstantPureChoice)*## Symbol.Div*# ")"#,
    EscSeq <- "\\"# ("\\" : "\"" : "n" : "t" : "z")##,
    Str <- "\""# ((EscSeq : !(("\\" : "\"")) .))*## "\""#,
    CharClass <- "["# (!"[" !"]" !Symbol.LineEnd (("\\[" : "\\]" : "\\\\" : .))##)+## "]"#,
    Wildcard <- ".",
}
